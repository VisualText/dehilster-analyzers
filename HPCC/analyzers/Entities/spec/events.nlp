###############################################
# FILE: events
# SUBJ: comment
# AUTH: David de Hilster
# CREATED: 2023-7-17 7:10:17
# MODIFIED:
###############################################

@PATH _ROOT _paragraph _sentence

# @RULES
# _this <- _xSTART _person @@

###############################################
# Isaiah Worlow, Steven Fluharty, and Tiffany Kerekes are charged 
###############################################

@POST
AddPeople(N(1),"event",N("$text",3));
single();

@RULES
_event <-
	_xWILD [plus match=(_titleCaps _person \, of and all _residentOf)]	### (1)
	_xWILD [plus match=(was is are were each)]							### (2)
	_xWILD [one match=(charged arrested indicted)]						### (3)
	@@

###############################################
# charging Ronald DiPietro, Thomas Helmick, and Jason and Rebecca Kachner. 
###############################################

@POST
AddPeople(N(2),"event","charged");
single();

@RULES
_event <-
	charging										### (1)
	_xWILD [plus match=(_titleCaps _person \, and)]	### (2)
	with											### (3)
	@@

###############################################
# One indictment charges that Stephanie Condric of Canton, Ohio
###############################################

@POST
AddPeople(N(4),"event","charged");
single();

@RULES
_event <-
	indictment										### (1)
	charges											### (2)
	that											### (3)
	_xWILD [plus match=(_titleCaps _person \, and)]	### (4)
	@@

	
###############################################
# People were previously assigned to case as TITLE
###############################################

@POST
AddPeople(N(1),"title",N("$text",4));
AddPeopleAttr(N(1),"event","assigned");
single();

@RULES
_event <-
	_xWILD [plus match=(_titleCaps _person \, and)]	### (1)
	_assigned										### (2)
	as												### (3)
	_title											### (4)
	@@