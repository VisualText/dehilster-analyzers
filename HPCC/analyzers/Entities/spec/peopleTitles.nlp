###############################################
# FILE: peopleTitles
# SUBJ: comment
# AUTH: David de Hilster
# CREATED: 2023-7-16 14:23:44
# MODIFIED:
###############################################

@PATH _ROOT _paragraph _sentence

###############################################
# Ronald DiPietro (a Certified Public Accountant)
###############################################

@POST
S("con") = AddPerson(N(1),0);
AddUniqueStr(S("con"),"title",N("title",2));
single();

@RULES
_person <-
	_person	### (1)
	_titleParen	### (2)
	@@

###############################################
# U.S. District Magistrate Judges Amanda M. Knapp of and Moose Manhold
###############################################

@PRE
<1,1> var("plural");

@POST
AddPeople(N(2),"title",N("$text",1));
single();

@RULES
_titleConj <-
	_title																		### (1)														### (2)
	_xWILD [plus match=(_titleCaps _person \, and all the _residentOf)] ### (2)	### (3)
	@@

###############################################
# U.S. District Magistrate Judge Amanda M. Knapp of the Northern District of Ohio
###############################################

@POST
if (N(3)) {
	S("con") = AddPerson(N(2),N(3));
} else {
	S("con") = AddPerson(N(2),0);
}
if (N("title",1))
	AddUniqueStr(S("con"),"title",N("title",1));
else
	AddUniqueStr(S("con"),"title",N("$text",1));
if (N("agency",1))
	AddUniqueStr(S("con"),"agency",N("agency",1));
single();

@RULES
_person <-
	_title			### (1)
	_person			### (3)
	_ofRegion [opt]	### (4)
	@@
